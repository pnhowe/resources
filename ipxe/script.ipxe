#!ipxe
:top

prompt --key s --timeout 5000 Press 's' for shell && goto do_shell ||

:getip
echo mac.........${net0/mac}
dhcp net0 || goto retry

echo platform:...${platform}
echo builtarch:..${buildarch}
echo hostname:...${net0/hostname}
echo Config UUID:${210:string}
echo ip address:.${net0/ip}
echo gateway ip:.${net0/gateway}
echo netmask:....${net0/netmask}
echo dns server:.${net0/dns}
echo domain:.....${net0/domain}
echo dhcp server:${net0/dhcp-server}
echo syslog svr.:${net0/syslog}
echo Routing Table:
route

echo Getting Boot Script

isset ${210} && goto w_config ||
chain http://contractor/config/boot_script/ || goto retry

:w_config
chain http://contractor/config/boot_script/c/${210:string} || goto retry

# the script didn't error, but also didn't go anywhere, hand it off to the EFI/BIOS
# NOTE: efi "normal" boot goes through here b/c it san boot the local disk
:fall_through
exit 1

:do_shell
echo Dropping to iPXE shell, docs at http://ipxe.org/cmd
echo You probably want to "dhcp net0" first
echo Type "exit" to continue with the boot process.
shell
goto top

:retry
sleep 10
goto getip
